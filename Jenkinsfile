pipeline {
    agent any

    triggers {
        githubPush()
    }

    environment {
        // íƒ€ì„ì¡´ ì„¤ì • (í•œêµ­ ì‹œê°„)
        TZ = 'Asia/Seoul'
        
        // Registry ì„¤ì •
        REGISTRY_URL = "registry.momuzzi.site"
        REGISTRY_CREDENTIALS_ID = "depromeet-registry"
        
        // Docker ì´ë¯¸ì§€ ì„¤ì •
        IMAGE_NAME = "depromeet-server-image"
        
        // NCP Server ì„¤ì •
        NCP_SERVER_CREDENTIALS_ID = "depromeet-server"
        NCP_SERVER_HOST = "api.momuzzi.site"
        NCP_SERVER_USER = "ubuntu"

        DEPLOY_PATH = "/home/ubuntu/momuzzi-server"
        
        // Kotlin ì»´íŒŒì¼ ìµœì í™”
        GRADLE_OPTS = "-Xmx4g -XX:MaxMetaspaceSize=512m"
        
        // Gradle ìºì‹œ ì„¤ì •
        GRADLE_USER_HOME = "${env.WORKSPACE}/.gradle"
    }
    
    tools {
        gradle 'gradle-8.14.3'
    }

    stages {
        stage('Checkout') {
            steps {
                checkout scm
                script {
                    env.GIT_COMMIT_SHORT = sh(
                        script: "git rev-parse --short HEAD",
                        returnStdout: true
                    ).trim()
                }
            }
        }
        
        stage('Setup Environment') {
            steps {
                script {
                    sh 'chmod +x ./gradlew'
                    sh './gradlew --stop || true'
                    sh 'pkill -f "KotlinCompileDaemon" || true'
                    
                    // Docker ì •ë¦¬(ì¼ì‹œì ìœ¼ë¡œ ì£¼ì„ ì²˜ë¦¬)
                    // sh 'docker system prune -f || true'
                    // sh 'docker builder prune -f || true'
                }
            }
        }
        stage('Test') {
            when {
                anyOf {
                    allOf {
                        changeRequest()
                        changeRequest target: 'dev'
                    }
                    branch 'main'
                }
            }
            environment {
                GRADLE_USER_HOME = "${env.WORKSPACE}/.gradle"
            }
            steps {
                timeout(time: 15, unit: 'MINUTES') {
                    retry(2) {
                        sh '''#!/bin/bash -eo pipefail
./gradlew test --parallel --no-daemon --stacktrace --build-cache | tee test-result.log
'''
                    }
                }
            }
            post {
                always {
                    script {
                        // í…ŒìŠ¤íŠ¸ ê²°ê³¼ ë¦¬í¬íŠ¸ ì €ì¥
                        def testResults = junit '**/build/test-results/test/*.xml'
                        
                        // í…ŒìŠ¤íŠ¸ ìš”ì•½ ì¶œë ¥
                        sh 'grep -E "Test result|BUILD SUCCESSFUL|BUILD FAILED" -A 5 test-result.log || true'
                        
                        // PRì—ë§Œ í…ŒìŠ¤íŠ¸ ê²°ê³¼ ì½”ë©˜íŠ¸ ì¶”ê°€
                        if (env.CHANGE_ID) {
                            def totalCount = testResults.totalCount
                            def passCount = testResults.passCount
                            def failCount = testResults.failCount
                            def skipCount = testResults.skipCount
                            
                            def status = failCount == 0 ? 'âœ… ì„±ê³µ' : 'âŒ ì‹¤íŒ¨'
                            def emoji = failCount == 0 ? 'ğŸ‰' : 'âš ï¸'
                            def buildUrl = env.BUILD_URL
                            
                            // ì‹¤íŒ¨í•œ í…ŒìŠ¤íŠ¸ ëª©ë¡ ì¶”ì¶œ
                            def failedTests = ""
                            if (failCount > 0) {
                                def failedTestsList = testResults.getFailedTests()
                                def failedTestsInfo = []
                                
                                failedTestsList.take(10).each { test ->
                                    def className = test.className.tokenize('.').last()
                                    def testName = test.name
                                    failedTestsInfo.add("- \`${className}.${testName}\`")
                                }
                                
                                if (failedTestsList.size() > 10) {
                                    failedTestsInfo.add("- ... ì™¸ ${failedTestsList.size() - 10}ê°œ")
                                }
                                
                                failedTests = """

### ${emoji} ì‹¤íŒ¨í•œ í…ŒìŠ¤íŠ¸
${failedTestsInfo.join('\n')}
"""
                            }
                            
                            // GitHub ë ˆí¬ì§€í† ë¦¬ ì •ë³´ ì¶”ì¶œ
                            def repoFullName = sh(
                                script: '''
                                    if [ -n "${CHANGE_URL}" ]; then
                                        echo "${CHANGE_URL}" | sed -E 's|https://github.com/([^/]+/[^/]+)/pull/.*|\\1|'
                                    else
                                        echo "${GIT_URL}" | sed -E 's|.*github.com[:/]([^/]+/[^.]+)(\\.git)?|\\1|'
                                    fi
                                ''',
                                returnStdout: true
                            ).trim()
                            
                            // ì½”ë©˜íŠ¸ ë‚´ìš©ì„ íŒŒì¼ë¡œ ì €ì¥ (JSON ì´ìŠ¤ì¼€ì´í•‘ ë¬¸ì œ íšŒí”¼)
                            def commentBody = """## ğŸ§ª í…ŒìŠ¤íŠ¸ ê²°ê³¼ ${status}

**ğŸ“Š í†µê³„**
- ì „ì²´: ${totalCount}ê°œ
- ì„±ê³µ: ${passCount}ê°œ âœ…
- ì‹¤íŒ¨: ${failCount}ê°œ ${failCount > 0 ? 'âŒ' : ''}
- ìŠ¤í‚µ: ${skipCount}ê°œ â­ï¸
${failedTests}

**ğŸ”— ë§í¬**
- [ìƒì„¸ í…ŒìŠ¤íŠ¸ ê²°ê³¼ ë³´ê¸°](${buildUrl}testReport/)
- [ë¹Œë“œ ë¡œê·¸ ë³´ê¸°](${buildUrl}console)

---
_Build #${env.BUILD_NUMBER} â€¢ ${new Date().format('yyyy-MM-dd HH:mm:ss KST')}_
"""
                            
                            writeFile file: 'pr-comment.txt', text: commentBody
                            
                            // GitHub APIë¥¼ í†µí•´ PR ì½”ë©˜íŠ¸ ì¶”ê°€
                            withCredentials([string(credentialsId: 'github-token', variable: 'GITHUB_TOKEN')]) {
                                sh """
                                    # JSON payload ìƒì„± (jqë¡œ ì•ˆì „í•˜ê²Œ ì¸ì½”ë”©)
                                    COMMENT_BODY=\$(cat pr-comment.txt | jq -Rs .)
                                    
                                    # GitHub API í˜¸ì¶œ
                                    RESPONSE=\$(curl -s -w "\\n%{http_code}" -X POST \\
                                      -H "Authorization: token \${GITHUB_TOKEN}" \\
                                      -H "Accept: application/vnd.github.v3+json" \\
                                      https://api.github.com/repos/${repoFullName}/issues/${env.CHANGE_ID}/comments \\
                                      -d "{\\"body\\":\${COMMENT_BODY}}")
                                    
                                    HTTP_CODE=\$(echo "\$RESPONSE" | tail -n1)
                                    RESPONSE_BODY=\$(echo "\$RESPONSE" | head -n-1)
                                    
                                    if [ "\$HTTP_CODE" -eq 201 ]; then
                                        echo "âœ… PR ì½”ë©˜íŠ¸ ì¶”ê°€ ì„±ê³µ"
                                    else
                                        echo "âš ï¸ PR ì½”ë©˜íŠ¸ ì¶”ê°€ ì‹¤íŒ¨ (HTTP \$HTTP_CODE)"
                                        echo "Response: \$RESPONSE_BODY"
                                    fi
                                """
                            }
                            
                            // ì„ì‹œ íŒŒì¼ ì •ë¦¬
                            sh 'rm -f pr-comment.txt'
                        }
                    }
                }
            }
        }

        stage('Build Application') {
            steps {
                script {
                    // ë¸Œëœì¹˜ ì •ë³´ ë””ë²„ê¹…
                    echo "Current branch: ${env.BRANCH_NAME}"
                    echo "Git branch: ${env.GIT_BRANCH}"
                    sh 'echo "Git branch from command: $(git branch --show-current)"'
                    sh 'echo "All git branches: $(git branch -a)"'

                    sh '''
                    ./gradlew :module-api:clean :module-api:bootJar \
                    --no-daemon \
                    --stacktrace \
                    -x test \
                    -Dorg.gradle.jvmargs="-Xmx1g -XX:MaxMetaspaceSize=512m" \
                    -Dkotlin.daemon.jvm.options="-Xmx512m,-XX:MaxMetaspaceSize=256m" \
                    -Dkotlin.incremental=false
                    '''
                }
            }
        }

        stage('Docker Build Test') {
            when {
                changeRequest()
            }
            steps {
                script {
                    sh '''
                        docker build -f module-api/Dockerfile -t test-build .
                        docker rmi test-build || true
                    '''
                }
            }
        }

        stage('Docker Build & Push') {
            steps {
                script {
                    def isMainBranch = env.BRANCH_NAME == 'main' ||
                                     env.GIT_BRANCH == 'origin/main' ||
                                     env.GIT_BRANCH == 'main' ||
                                     sh(script: 'git branch --show-current', returnStdout: true).trim() == 'main'

                    if (isMainBranch) {
                        // Docker ìºì‹œ ì •ë¦¬ (ì†ìƒëœ ë ˆì´ì–´ ì œê±°)
                        sh """
                            docker system prune -af
                            docker builder prune -af
                        """

                        def imageTag = "${env.GIT_COMMIT_SHORT}"
                        def fullImageName = "${REGISTRY_URL}/${IMAGE_NAME}"

                        // Docker ì´ë¯¸ì§€ ë¹Œë“œ (ìºì‹œ ì‚¬ìš© ì•ˆí•¨)
                        sh """
                            docker build --no-cache -f module-api/Dockerfile -t ${fullImageName}:${imageTag} .
                            docker tag ${fullImageName}:${imageTag} ${fullImageName}:latest
                        """

                        // Registryì— ë¡œê·¸ì¸ ë° ì´ë¯¸ì§€ í‘¸ì‹œ
                        withCredentials([usernamePassword(
                            credentialsId: "${REGISTRY_CREDENTIALS_ID}",
                            usernameVariable: 'REGISTRY_USERNAME',
                            passwordVariable: 'REGISTRY_PASSWORD'
                        )]) {
                            sh """
                                # Docker ë¡œê·¸ì•„ì›ƒ í›„ ì¬ë¡œê·¸ì¸
                                docker logout ${REGISTRY_URL} || true

                                echo "Attempting login to ${REGISTRY_URL} with user: \$REGISTRY_USERNAME"
                                echo \$REGISTRY_PASSWORD | docker login ${REGISTRY_URL} -u \$REGISTRY_USERNAME --password-stdin

                                # ì´ë¯¸ì§€ ì •ë³´ í™•ì¸
                                docker images | grep ${fullImageName}

                                # Push ì‹œë„
                                docker push ${fullImageName}:${imageTag}
                                docker push ${fullImageName}:latest
                            """
                        }

                        // ë¡œì»¬ ì´ë¯¸ì§€ ì •ë¦¬
                        sh """
                            docker rmi ${fullImageName}:${imageTag} || true
                            docker rmi ${fullImageName}:latest || true
                        """
                    } else {
                        echo "Skipping Docker Build & Push - not main branch"
                    }
                }
            }
        }

        stage('Deploy to NCP Server') {
            steps {
                script {
                    def isMainBranch = env.BRANCH_NAME == 'main' ||
                                     env.GIT_BRANCH == 'origin/main' ||
                                     env.GIT_BRANCH == 'main' ||
                                     sh(script: 'git branch --show-current', returnStdout: true).trim() == 'main'

                    if (isMainBranch) {
                        withCredentials([usernamePassword(
                            credentialsId: "${REGISTRY_CREDENTIALS_ID}",
                            usernameVariable: 'REGISTRY_USERNAME',
                            passwordVariable: 'REGISTRY_PASSWORD'
                        )]) {
                            sshagent(credentials: ["${NCP_SERVER_CREDENTIALS_ID}"]) {
                                sh """
                                    ssh -o StrictHostKeyChecking=no ${NCP_SERVER_USER}@${NCP_SERVER_HOST} << 'EOF'
export REGISTRY_USERNAME="${REGISTRY_USERNAME}"
export REGISTRY_PASSWORD="${REGISTRY_PASSWORD}"
export REGISTRY_URL="${REGISTRY_URL}"
export IMAGE_NAME="${IMAGE_NAME}"
export DEPLOY_PATH="${DEPLOY_PATH}"

# ë°°í¬ ë””ë ‰í† ë¦¬ë¡œ ì´ë™
cd \${DEPLOY_PATH}

# Git ìµœì‹  ì½”ë“œ pull
git fetch origin
git reset --hard origin/main

# Docker ë„¤íŠ¸ì›Œí¬ê°€ ì—†ìœ¼ë©´ ìƒì„±
docker network ls | grep depromeet_network || docker network create depromeet_network

# Registry ì´ë¯¸ì§€ ì¡´ì¬ ì—¬ë¶€ í™•ì¸
REGISTRY_IMAGE_EXISTS=false

# Registryì— ë¡œê·¸ì¸ ì‹œë„
if echo "\${REGISTRY_PASSWORD}" | docker login \${REGISTRY_URL} -u "\${REGISTRY_USERNAME}" --password-stdin; then
    # ì´ë¯¸ì§€ ì¡´ì¬ ì—¬ë¶€ í™•ì¸ (HTTPS ì‚¬ìš©)
    if docker pull \${REGISTRY_URL}/\${IMAGE_NAME}:latest > /dev/null 2>&1; then
        echo "Registry image found, using registry image"
        REGISTRY_IMAGE_EXISTS=true
    else
        echo "Registry image not found, will build locally"
    fi
else
    echo "Registry login failed, will build locally"
fi

# ì• í”Œë¦¬ì¼€ì´ì…˜ ì„œë¹„ìŠ¤ ì¬ì‹œì‘
docker-compose -f docker-compose.prod.yml stop backend nginx || true
docker-compose -f docker-compose.prod.yml rm -f backend nginx || true

if [ "\$REGISTRY_IMAGE_EXISTS" = true ]; then
    # Registry ì´ë¯¸ì§€ë¥¼ backend:latestë¡œ íƒœê·¸ í›„ ë°°í¬
    docker pull \${REGISTRY_URL}/\${IMAGE_NAME}:latest
    docker tag \${REGISTRY_URL}/\${IMAGE_NAME}:latest backend:latest
fi

# Backend ë¨¼ì € ì‹œì‘
docker-compose -f docker-compose.prod.yml up -d backend

# Backendê°€ healthy ìƒíƒœê°€ ë  ë•Œê¹Œì§€ ëŒ€ê¸° (ìµœëŒ€ 2ë¶„)
echo "Waiting for backend to be healthy..."
for i in {1..24}; do
    if docker inspect --format='{{.State.Health.Status}}' backend 2>/dev/null | grep -q "healthy"; then
        echo "Backend is healthy"
        break
    fi
    echo "Waiting... (\$i/24)"
    sleep 5
done

# Nginx ì‹œì‘
docker-compose -f docker-compose.prod.yml up -d nginx

# ì‚¬ìš©í•˜ì§€ ì•ŠëŠ” ì´ë¯¸ì§€ ì •ë¦¬
docker image prune -af --filter "until=24h"

# ë°°í¬ ìƒíƒœ í™•ì¸
sleep 10
docker ps
echo "Checking service health..."
docker inspect --format='{{.Name}}: {{.State.Health.Status}}' backend nginx || true
EOF
                                """
                            }
                        }
                    } else {
                        echo "Skipping Deploy to NCP Server - not main branch"
                    }
                }
            }
        }
    }
    
    post {
        always {
            // Gradle daemon ì •ë¦¬
            sh './gradlew --stop || true'
            // .gradle ìºì‹œëŠ” ë³´ì¡´í•˜ë©´ì„œ ì›Œí¬ìŠ¤í˜ì´ìŠ¤ ì •ë¦¬
            cleanWs patterns: [[pattern: '.gradle/**', type: 'EXCLUDE']]
        }
        success {
            echo 'Pipeline succeeded!'
        }
        failure {
            echo 'Pipeline failed!'
        }
    }
}
